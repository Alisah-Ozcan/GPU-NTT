cmake_minimum_required(VERSION 3.26)
project(GPUNTT VERSION 1.0 LANGUAGES C CXX CUDA ASM)

set(BINARY_LOCATION ${CMAKE_CURRENT_BINARY_DIR}/bin)

include(GNUInstallDirs)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CUDA_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

find_package(CUDAToolkit REQUIRED)

if(NOT DEFINED CMAKE_CUDA_ARCHITECTURES OR CMAKE_CUDA_ARCHITECTURES STREQUAL "")
    set(CMAKE_CUDA_ARCHITECTURES 80;86;89;90 CACHE STRING "CUDA GPU architectures" FORCE)
endif()
message(STATUS "Setting CMAKE_CUDA_ARCHITECTURES to ${CMAKE_CUDA_ARCHITECTURES} for GPUNTT")

##################
# Export Library #
##################

set(RUNTIME_DESTINATION ${CMAKE_INSTALL_BINDIR})
set(LIBRARY_DESTINATION ${CMAKE_INSTALL_LIBDIR})
set(ARCHIVE_DESTINATION ${CMAKE_INSTALL_LIBDIR})
set(INCLUDES_DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})
set(INCLUDES_INSTALL_DIR ${INCLUDES_DESTINATION}/GPUNTT-${PROJECT_VERSION})

set(GPUNTT_GENERATED_DIR "${CMAKE_CURRENT_BINARY_DIR}/generated")
set(GPUNTT_VERSION_CONFIG "${GPUNTT_GENERATED_DIR}/${PROJECT_NAME}ConfigVersion.cmake")
set(GPUNTT_PROJECT_CONFIG "${GPUNTT_GENERATED_DIR}/${PROJECT_NAME}Config.cmake")
set(GPUNTT_TARGETS_EXPORT_NAME "${PROJECT_NAME}Targets")
set(GPUNTT_CONFIG_INSTALL_DIR "${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_NAME}-${PROJECT_VERSION}")
set(GPUNTT_NAMESPACE "${PROJECT_NAME}::")

include(CMakePackageConfigHelpers)
write_basic_package_version_file(
    "${GPUNTT_VERSION_CONFIG}" VERSION ${PROJECT_VERSION} COMPATIBILITY SameMajorVersion
)
configure_file("${GPUNTT_SOURCE_DIR}/cmake/Config.cmake.in" "${GPUNTT_PROJECT_CONFIG}" @ONLY)

install(
    FILES "${GPUNTT_PROJECT_CONFIG}" "${GPUNTT_VERSION_CONFIG}"
    DESTINATION "${GPUNTT_CONFIG_INSTALL_DIR}")

install(
    EXPORT "${GPUNTT_TARGETS_EXPORT_NAME}"
    NAMESPACE "${GPUNTT_NAMESPACE}"
    DESTINATION "${GPUNTT_CONFIG_INSTALL_DIR}")

add_subdirectory(src)

############
# Examples #
############

option(GPUNTT_BUILD_EXAMPLES "Build GPU-NTT Examples" OFF)
message(STATUS "GPUNTT_BUILD_EXAMPLES: ${GPUNTT_BUILD_EXAMPLES}")
if(GPUNTT_BUILD_EXAMPLES)
    add_subdirectory(example)
endif()

##############
# BENCHMARKS #
##############

option(GPUNTT_BUILD_BENCHMARKS "Build GPU-NTT Benchmarks" OFF)
message(STATUS "GPUNTT_BUILD_BENCHMARKS: ${GPUNTT_BUILD_BENCHMARKS}")
if(GPUNTT_BUILD_BENCHMARKS)
    add_subdirectory(benchmark)
endif()
